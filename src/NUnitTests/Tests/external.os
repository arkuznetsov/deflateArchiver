// ----------------------------------------------------------
// Use of this source code is governed by an MIT-style
// license that can be found in the LICENSE file or at
// https://opensource.org/licenses/MIT.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/oscript-deflate/
// ----------------------------------------------------------

// #Использовать "build"

Перем юТест;

////////////////////////////////////////////////////////////////////
// Программный интерфейс

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт

	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_СоздатьКомпоненту");
	ВсеТесты.Добавить("ТестДолжен_УпаковатьПоток");
	ВсеТесты.Добавить("ТестДолжен_РаспаковатьПоток");
	ВсеТесты.Добавить("ТестДолжен_УпаковатьДанные");
	ВсеТесты.Добавить("ТестДолжен_РаспаковатьДанные");
	ВсеТесты.Добавить("ТестДолжен_УпаковатьФайл");
	ВсеТесты.Добавить("ТестДолжен_РаспаковатьФайл");

	ПередЗапускомТестов();

	Возврат ВсеТесты;

КонецФункции // ПолучитьСписокТестов()

Процедура ПередЗапускомТестов()

	ПутьККомпоненте = ОбъединитьПути(ТекущийСценарий().Каталог, "src", "oscript-deflate", "bin");
	ПутьККомпоненте = ОбъединитьПути(ПутьККомпоненте, "Debug", "net452", "oscript-deflate.dll");
	
	Попытка
		ПодключитьВнешнююКомпоненту(ПутьККомпоненте);
		Сообщить("Компонента подключена.");
	Исключение
		Сообщить("Компонента подключена ранее!");
	КонецПопытки;

КонецПроцедуры // ПередЗапускомТестов()

Процедура ТестДолжен_СоздатьКомпоненту() Экспорт

	Упаковщик = Новый УпаковщикDeflate();
	юТест.ПроверитьРавенство(ТипЗнч(Упаковщик), Тип("УпаковщикDeflate"), "Не удалось создать компоненту УпаковщикDeflate"); 

КонецПроцедуры // ТестДолжен_СоздатьКомпоненту()

Процедура ТестДолжен_УпаковатьПоток() Экспорт

	Упаковщик = Новый УпаковщикDeflate();
	юТест.ПроверитьРавенство(ТипЗнч(Упаковщик), Тип("УпаковщикDeflate"), "Не удалось создать компоненту УпаковщикDeflate"); 

	ВходящийПоток = Новый ПотокВПамяти();
	ЗаписьДанных = Новый ЗаписьДанных(ВходящийПоток, КодировкаТекста.UTF8);
	ЗаписьДанных.ЗаписатьСимволы("Это текст для тестирования упаковки потока!");
	ЗаписьДанных.Закрыть();
	ВходящийПоток.Перейти(0, ПозицияВПотоке.Начало);

	ИсходящийПоток = Новый ПотокВПамяти();

	Упаковщик.УпаковатьПоток(ВходящийПоток, ИсходящийПоток, 2);

	Сообщить(СтрШаблон("Исходный размер: %1", ВходящийПоток.Размер()));
	Сообщить(СтрШаблон("Сжатый размер: %1", ИсходящийПоток.Размер()));

	ВходящийПоток.Закрыть();
	ДД = ИсходящийПоток.ЗакрытьИПолучитьДвоичныеДанные();
	Сообщить(СтрШаблон("Сжатые данные: %1", Base64Строка(ДД)));
	
	ТестоваяСтрока = "HYzRCYAwEENXOTfVOoCTCEUUyknPFd5tZNqfkPcC4cxCmOLBc8ti3Lx5TDOYlivBRaWra9j5BD6l00wY40RQlx8=";

	юТест.ПроверитьРавенство(Base64Строка(ДД), ТестоваяСтрока, "Ошибка при упаковке потока"); 

КонецПроцедуры // ТестДолжен_УпаковатьПоток()

Процедура ТестДолжен_РаспаковатьПоток() Экспорт

	Упаковщик = Новый УпаковщикDeflate();
	юТест.ПроверитьРавенство(ТипЗнч(Упаковщик), Тип("УпаковщикDeflate"), "Не удалось создать компоненту УпаковщикDeflate"); 

	СжатыеДанные = "HYzRCYAwEENXqZu2XcBJhEMUyonnCi8bee1PyHuBcKgTJePG1dQLF4/2ZSYzVAlOjDd7DhVT40vha3BGSYx5lGDbDw==";
	ДД = Base64Значение(СжатыеДанные);

	ВходящийПоток = Новый ПотокВПамяти();
	ДД.ОткрытьПотокДляЧтения().КопироватьВ(ВходящийПоток);
	ВходящийПоток.Перейти(0, ПозицияВПотоке.Начало);

	ИсходящийПоток = Новый ПотокВПамяти();

	Упаковщик.РаспаковатьПоток(ВходящийПоток, ИсходящийПоток);

	Сообщить(СтрШаблон("Исходный размер: %1", ВходящийПоток.Размер()));
	Сообщить(СтрШаблон("Распакованный размер: %1", ИсходящийПоток.Размер()));

	ЧтениеДанных = Новый ЧтениеДанных(ИсходящийПоток, КодировкаТекста.UTF8);
	ИсходящийПоток.Перейти(0, ПозицияВПотоке.Начало);
	РаспакованыйТекст = ЧтениеДанных.ПрочитатьСимволы();

	ВходящийПоток.Закрыть();
	ИсходящийПоток.Закрыть();

	ТестоваяСтрока = "Это текст для тестирования распаковки потока!";

	юТест.ПроверитьРавенство(РаспакованыйТекст, ТестоваяСтрока, "Ошибка при распаковке потока"); 

КонецПроцедуры // ТестДолжен_РаспаковатьПоток()

Процедура ТестДолжен_УпаковатьДанные() Экспорт

	Упаковщик = Новый УпаковщикDeflate();
	юТест.ПроверитьРавенство(ТипЗнч(Упаковщик), Тип("УпаковщикDeflate"), "Не удалось создать компоненту УпаковщикDeflate"); 

	ВходящийПоток = Новый ПотокВПамяти();
	ЗаписьДанных = Новый ЗаписьДанных(ВходящийПоток, КодировкаТекста.UTF8);
	ЗаписьДанных.ЗаписатьСимволы("Это текст для тестирования упаковки двоичных данных!");
	
	ВходящиеДанные = ВходящийПоток.ЗакрытьИПолучитьДвоичныеДанные();

	ИсходящиеДанные = Упаковщик.УпаковатьДанные(ВходящиеДанные, 2);

	ТестоваяСтрока = "HYvBCYAwEARbiZ1qRPLwYSWCBIUQSWxhtiPv8jl2Zvc4FenBzkPVohi4eXUM40zRTCdz0SxbsfIZ1CErxR+yQVGiadfmwscDph8=";

	юТест.ПроверитьРавенство(Base64Строка(ИсходящиеДанные), ТестоваяСтрока, "Ошибка при упаковке двоичных данных"); 

КонецПроцедуры // ТестДолжен_УпаковатьДанные()

Процедура ТестДолжен_РаспаковатьДанные() Экспорт

	Упаковщик = Новый УпаковщикDeflate();
	юТест.ПроверитьРавенство(ТипЗнч(Упаковщик), Тип("УпаковщикDeflate"), "Не удалось создать компоненту УпаковщикDeflate"); 

	СжатыеДанные = "HYvBCYAwEARb0U6TgPjwYSVCCAohktjCbEfe5bPszLJcSozF4qEpKi3cvDqncaYqMChkunUbAlmRz0SbQ6P6qRhU7XQd2lz4YcL6Aw==";
	ВходящиеДанные = Base64Значение(СжатыеДанные);

	ИсходящиеДанные = Упаковщик.РаспаковатьДанные(ВходящиеДанные);

	ИсходящийПоток = ИсходящиеДанные.ОткрытьПотокДляЧтения();
	ЧтениеДанных = Новый ЧтениеДанных(ИсходящийПоток, КодировкаТекста.UTF8);
	ИсходящийПоток.Перейти(0, ПозицияВПотоке.Начало);
	РаспакованыйТекст = ЧтениеДанных.ПрочитатьСимволы();

	ТестоваяСтрока = "Это текст для тестирования распаковки двоичных данных!";

	юТест.ПроверитьРавенство(РаспакованыйТекст, ТестоваяСтрока, "Ошибка при распаковке двоичных данных"); 

КонецПроцедуры // ТестДолжен_РаспаковатьДанные()

Процедура ТестДолжен_УпаковатьФайл() Экспорт

	Упаковщик = Новый УпаковщикDeflate();
	юТест.ПроверитьРавенство(ТипЗнч(Упаковщик), Тип("УпаковщикDeflate"), "Не удалось создать компоненту УпаковщикDeflate"); 

	ПутьКТестовомуКаталогу = ОбъединитьПути(ТекущийСценарий().Каталог, "test");
	ОбеспечитьКаталог(ПутьКТестовомуКаталогу);

	ИмяТестовогоФайла = "testFile1.txt";
	ПутьКИсходномуФайлу = ОбъединитьПути(ПутьКТестовомуКаталогу, ИмяТестовогоФайла);
	ПутьКТестовомуФайлу = ПутьКИсходномуФайлу + ".out";

	Текст = Новый ТекстовыйДокумент();
	Текст.УстановитьТекст("Это тестовый файл для тестирования сжатия файла!");
	Текст.Записать(ПутьКИсходномуФайлу, КодировкаТекста.UTF8);

	Упаковщик.УпаковатьФайл(ПутьКИсходномуФайлу, ПутьКТестовомуФайлу, 2);

	ДД = Новый ДвоичныеДанные(ПутьКТестовомуФайлу);

	Сообщить(СтрШаблон("Содержимое упакованного файла: %1", Base64Строка(ДД)));

	ТестоваСтрока = "e797/4W1F5su7FMAElsvNoKYFzZd7L6wU+Fiy4UNF3Ze2K1wYcuF3Rf7EQp2XGwAKQLK7gWygRKNF7Zd2ACW6IfrurBBkZcLAA==";

	юТест.ПроверитьРавенство(Base64Строка(ДД), ТестоваСтрока, "Ошибка при упаковке файла");

	УдалитьФайлы(ПутьКИсходномуФайлу);
	УдалитьФайлы(ПутьКТестовомуФайлу);
	УдалитьФайлы(ПутьКТестовомуКаталогу);

КонецПроцедуры // ТестДолжен_УпаковатьФайл()

Процедура ТестДолжен_РаспаковатьФайл() Экспорт

	Упаковщик = Новый УпаковщикDeflate();
	юТест.ПроверитьРавенство(ТипЗнч(Упаковщик), Тип("УпаковщикDeflate"), "Не удалось создать компоненту УпаковщикDeflate"); 

	ПутьКТестовомуКаталогу = ОбъединитьПути(ТекущийСценарий().Каталог, "test");
	ОбеспечитьКаталог(ПутьКТестовомуКаталогу);

	ИмяТестовогоФайла = "testFile1.dfl";
	ПутьКИсходномуФайлу = ОбъединитьПути(ТекущийСценарий().Каталог, "test", ИмяТестовогоФайла);
	ПутьКТестовомуФайлу = ПутьКИсходномуФайлу + ".out";

	ДанныеФайла = "e797/4W1F5su7FMAElsvNoKYFzZd7L6wU+Fiy4UNF3Ze2K1wYcuF3Rf7EQp2XGwAKQLK7gWygRINFzZcbLywHyiwCyyx68IOuO4LGxR5uQA=";

	ДД = Base64Значение(ДанныеФайла);
	ДД.Записать(ПутьКИсходномуФайлу);

	Упаковщик.РаспаковатьФайл(ПутьКИсходномуФайлу, ПутьКТестовомуФайлу);

	Текст = Новый ТекстовыйДокумент();
	Текст.Прочитать(ПутьКТестовомуФайлу, КодировкаТекста.UTF8);

	ТестоваяСтрока = "Это тестовый файл для тестирования распаковки файла!";

	юТест.ПроверитьРавенство(СокрЛП(Текст.ПолучитьТекст()), ТестоваяСтрока, "Ошибка при распаковке файла");

	УдалитьФайлы(ПутьКИсходномуФайлу);
	УдалитьФайлы(ПутьКТестовомуФайлу);
	УдалитьФайлы(ПутьКТестовомуКаталогу);

КонецПроцедуры // ТестДолжен_УпаковатьФайл()

Процедура ОбеспечитьКаталог(ПутьККаталогу)

	ВремКаталог = Новый Файл(ПутьККаталогу);
	Если НЕ (ВремКаталог.Существует() И ВремКаталог.ЭтоКаталог()) Тогда
		СоздатьКаталог(ПутьККаталогу);
	КонецЕсли;

КонецПроцедуры // ОбеспечитьКаталог()